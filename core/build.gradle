plugins {
    id 'java-library'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + stubs.output + test.output
            // TODO: quinton: surely this is implicit and can be removed?
            srcDir file('src/integrationTest/java')
        }
        // TODO: quinton: surely this is implicit and can be removed?
        resources.srcDir file('src/integrationTest/resources')
    }
    /*
     * Generate integration test stubs into a separate source set, to avoid circular dependencies etc.
     */
    integrationTestStubs {
        java {
            compileClasspath += main.output + test.output + integrationTest.output
            runtimeClasspath += main.output + stubs.output + test.output
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestStubsCompile.extendsFrom testCompile
    integrationTestStubsRuntime.extendsFrom testRuntime

}

dependencies {
    compile project(':dependencies:java.rmi')
    compile 'org.json:json:20171018'
    // We compile using the GraalVM JDK. Please follow the instructions in the quick start guide at
    // ../docs/GettingStarted.md to set up GraalVM properly.

    compile 'org.graalvm.sdk:graal-sdk:' + project.property('graalVmVersion')
    compile 'junit:junit:4.12'
    compile 'org.yaml:snakeyaml:1.23'
    compile 'com.github.pcj:google-options:1.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    // Added to use the ExpectedSystemExit rule to test code that calls System.exit(), added in KSTest.java
    testCompile 'com.github.stefanbirkner:system-rules:1.2.0'

    // TODO: quinton: the following 2 lines shouldn't be necessary
    // But if removed, 4 integration tests fail with the following types of exception.
    // Try to figure out why, and fix the problem so that these lines can be removed.
    //    amino.run.policy.ConsensusDMIntegrationTest > testConsensusDM FAILED
    //        amino.run.common.MicroServiceCreationException at ConsensusDMIntegrationTest.java:42
    //            Caused by: java.lang.NullPointerException
    testCompile project(':examples:helloworld')
    integrationTestCompile project(':examples:helloworld')
}

// Maven Plugin Properties
// Reset maven properties to ensure that their values are 
// consistent with the values used by Maven Publish Plugin
group = project.property('mavenGroupId')
version =  project.property('mavenVersion')

/*
   Return a string containing the GraalVM version, e.g. "1.0.0-rc8" returned by 'java -version'
   NOTE: This is a very clunky way of executing 'java -version | grep GraalVM | awk {print $2}'
         I could not find an easier way to do it in gradle - see below
         Note that java outputs version info to stderr, not stdout
*/
def getGraalVmVersion = { ->
    def javaErrOut = new PipedOutputStream()
    def grepIn = new PipedInputStream(javaErrOut)
    def grepOut = new PipedOutputStream()
    def awkIn = new PipedInputStream(grepOut)
    def awkOut = new ByteArrayOutputStream()
    exec {
        commandLine 'java', '-version'
        errorOutput = javaErrOut
    }
    exec {
        commandLine 'grep', 'GraalVM'
        standardInput = grepIn
        standardOutput = grepOut
    }
    exec {
        commandLine 'awk', '{print $2}'
        standardInput = awkIn
        standardOutput = awkOut
    }
    return awkOut.toString().trim()
}

// Check that the installed GraalVM version matches the the standard version we use for this project.
task checkGraalVmVersion {
    inputs.property('graalVmVersion', 'null')
    inputs.property('actualVersion', { getGraalVmVersion() })
    doLast {
        def requiredVersion = project.property('graalVmVersion')
        def actualVersion =  getGraalVmVersion()
        if (!requiredVersion.equals(actualVersion)) {
            throw new GradleException("Incorrect GraalVM version '" + actualVersion + "' installed. Version '" + requiredVersion + "' required.")
        }
    }
}

// Customize DM stub generation
genStubs {
    def pkg = 'amino.run.policy'
    def src = file(project.buildDir.toString() + '/classes/java/main/amino/run/policy/')
    def dst = file(project.projectDir.toString() + '/src/stubs/java/amino/run/policy/stubs/')
    args src.toString(), pkg, dst.toString()
    outputs.dir dst // Declare outputs, so gradle will run if they have been changed
    inputs.dir src   // Declare inputs, so gradle will run if they have been changed
}

task compileSampleSO(type: JavaCompile) {
    source = file(project.projectDir.toString() + '/src/test/java/amino/run/sampleSO')
    exclude('**/*_Stub*')  // Added for excluding older Stub files, while compiling Sample SO package.
    classpath = sourceSets.main.runtimeClasspath
    destinationDir = sourceSets.test.output.classesDirs[0]
    options.incremental = true
}

task genUnitTestStubs(type :JavaExec){
    dependsOn compileSampleSO
    main = "amino.run.compiler.StubGenerator"
    classpath sourceSets.test.output.classesDirs

    def pkg = 'amino.run.sampleSO'
    def src = file(project.buildDir.toString() + '/classes/java/test/amino/run/sampleSO/')
    def dst = file(project.projectDir.toString() + '/src/test/java/amino/run/sampleSO/stubs/')
    args src.toString(), pkg, dst.toString()
    inputs.dir src
    outputs.dir dst
}

task compileSampleSOStubs(type: JavaCompile) {
    dependsOn genUnitTestStubs
    source = file(project.projectDir.toString() + '/src/test/java/amino/run/sampleSO/stubs')
    classpath = sourceSets.main.runtimeClasspath
    classpath += sourceSets.test.output
    destinationDir = sourceSets.test.output.classesDirs[0]
    options.incremental = true
}

// Task for Graal Test Stubs Generation:
// Run `gradlew genGraalTestStubs` to generate Graal test stub files
task genGraalTestStubs(type: JavaExec) {
    dependsOn compileJava
    main = "amino.run.compiler.GraalStubGenerator"
    def microServiceDir = "$projectDir/src/test/resources/"
    def microServiceFile = "student.js"
    def microServicePath = microServiceDir + microServiceFile
    def outPath = "$projectDir/src/test/java"
    def packageName = "amino.run.stubs"
    def microServiceClasses = "Student"
    args microServicePath, outPath, packageName, microServiceClasses
    inputs.dir microServiceDir   // Declare inputs, so gradle will run if they have been changed
    outputs.dir outPath + "/amino/run/stubs" // Declare outputs, so gradle will run if they have been changed
}
// Task for Graal Test stubs compilation
task compileGraalTestStubs(type: JavaCompile) {
    dependsOn genGraalTestStubs
    source = "$projectDir/src/test/java/amino/run/stubs"
    classpath = sourceSets.main.runtimeClasspath
    destinationDir = sourceSets.test.output.classesDirs[0]
    options.incremental = true
    outputs.dir destinationDir
}

task compileIntegTestDemoPkg(type: JavaCompile) {
    source = file(project.projectDir.toString() + '/src/integrationTest/java/amino/run/demo/')
    exclude('**/*_Stub*')  // Added for excluding older Stub files, while compiling Integration Test Demo package.
    classpath = sourceSets.integrationTest.compileClasspath
    destinationDir = sourceSets.integrationTest.output.classesDirs[0]
    options.incremental = true
}

task genIntegTestStubs(type :JavaExec) {
    dependsOn compileIntegTestDemoPkg
    main = "amino.run.compiler.StubGenerator"
    classpath sourceSets.integrationTest.output.classesDirs

    def pkg = 'amino.run.demo'
    def src = file(project.buildDir.toString() + '/classes/java/integrationTest/amino/run/demo')
    def dst = file(project.projectDir.toString() + '/src/integrationTestStubs/java/amino/run/demo/stubs')
    args src.toString(), pkg, dst.toString()
    inputs.dir src // Declare inputs, so gradle will run if they have been changed
    outputs.dir dst // Declare outputs, so gradle will run if they have been changed
}

task integTest(type: Test) {
    dependsOn compileIntegrationTestStubsJava
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    classpath += sourceSets.integrationTestStubs.output
}

clean {
    delete genUnitTestStubs.outputs.files // Add generated stubs to what the clean task must delete
    delete genGraalTestStubs.outputs.files
    delete genIntegTestStubs.outputs.files

    doLast {
        logger.info('Clean will delete ' + clean.targetFiles.asCollection())
    }
}

jar {
    baseName project.property('mavenArtifactId')
}

compileJava {
    dependsOn checkGraalVmVersion
}

compileTestJava {
    dependsOn compileStubsJava
    dependsOn compileGraalTestStubs
    dependsOn stubsClasses
    classpath += sourceSets.stubs.output
}

test {
    dependsOn compileSampleSOStubs
}


compileIntegrationTestJava {
    dependsOn stubsClasses
}

compileIntegrationTestStubsJava {
    dependsOn genIntegTestStubs
}

integTest.mustRunAfter test
check.dependsOn integTest
