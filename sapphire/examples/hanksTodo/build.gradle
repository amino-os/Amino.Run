apply plugin: 'java-library'

dependencies {
    compile project(':sapphire-core')
}

// Task for Stub compilation
task compileStubs(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

// Task for app stub generation
task genStubs(type: JavaExec) {
    main = "sapphire.compiler.StubGenerator"
    classpath = sourceSets.main.runtimeClasspath
    def pkgName = 'sapphire.appexamples.hankstodo'
    def src = "$buildDir/classes/java/main/sapphire/appexamples/hankstodo"
    def dst = "src/main/java/sapphire/appexamples/hankstodo/stubs/"
    args src, pkgName, dst
}

clean.doFirst {
    delete fileTree("$projectDir/src/main/java/sapphire/appexamples/hankstodo/stubs") {
        include '**/*_Stub.java'
    }
    println "Deleted $projectDir/src/main/java/sapphire/appexamples/hankstodo/stubs"
}

genStubs.mustRunAfter compileJava
compileStubs.dependsOn genStubs
jar.dependsOn compileStubs

// Start OMS with "gradlew runoms"
task runoms(type: JavaExec) {
    dependsOn jar
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.oms.OMSServerImpl'
    args project.property('omsIp'), project.property('omsPort')
}

// Start kernel server with "gradlew runks"
task runks(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')
}

// Task for run hanksTodoApp demo app
// Usage: gradlew runapp -PW=DisneyWorld
task runapp(type: JavaExec) {
    main = "sapphire.appexamples.hankstodo.hanskTododMain"
    classpath = sourceSets.main.runtimeClasspath
    args project.property('omsIp'), project.property('omsPort'), project.property('kernelIpAndroid'), project.property('kernelServerPort')
}
