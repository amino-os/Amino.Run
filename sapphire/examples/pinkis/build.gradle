apply plugin: 'java-library'

dependencies {
    compile project(':sapphire-core')
}

// Task for app stub generation
task genStubs(type: JavaExec) {
    main = "sapphire.compiler.StubGenerator"
    classpath = sourceSets.main.runtimeClasspath
    def pkgName = 'sapphire.demo'
    def src = "$buildDir/classes/java/main/sapphire/demo/"
    def dst = "$projectDir/src/main/java/sapphire/demo/stubs/"
    args src, pkgName, dst
    outputs.dir dst // Declare outputs, so gradle will run if they have been changed
    inputs.dir src   // Declare inputs, so gradle will run if they have been changed
}

task compileStubs(type: JavaCompile) {
    inputs.dir genStubs.outputs // Try to convince gradle to only recompile stubs when they've been regenerated.
    outputs.dir "$buildDir/classes/java/main/sapphire/appexamples/demo/stubs/" // ... or when someone messed with the compiled stubs
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

jar.dependsOn compileStubs

// task to start OMS
task runoms(type: JavaExec) {
    dependsOn jar
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.oms.OMSServerImpl'
    args project.property('omsIp'), project.property('omsPort')
}

// task to start kernel server (hosting the SO)
task runks(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')
}

// task to start more kernel server (hosting the SO)
task runks2(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort2'), project.property('omsIp'), project.property('omsPort')
}

task runks3(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort3'), project.property('omsIp'), project.property('omsPort')
}

// task for run client
task runapp(type: JavaExec) {
    main = "sapphire.demo.pinkisClient"
    classpath = sourceSets.main.runtimeClasspath
    args project.property('omsIp'), project.property('omsPort')
}
