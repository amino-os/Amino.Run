import dcap.sapphire.gradle.PolicyStubGenerationTask

plugins {
    // Google Java Format Plugin:
    // 1. Run `gradlew goJF` to format source codes
    // 2. Run `gradlew verGJF` to verify code farmat
    // 3. More usage information can be found at 
    // https://github.com/sherter/google-java-format-gradle-plugin
    id 'com.github.sherter.google-java-format' version '0.7.1'
    id 'java-library'
}

googleJavaFormat {
    options style: 'AOSP'
}
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
    test.java.srcDirs += 'src/integrationTest/java'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}
dependencies {
    compile 'org.json:json:20171018'
    compile project(':dependencies:java.rmi')
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile project(':examples:helloworld')
    integrationTestCompile project(':examples:helloworld')
}


// Maven Plugin Properties
// Reset maven properties to ensure that their values are 
// consistent with the values used by Maven Publish Plugin
group = project.property('mavenGroupId')
version =  project.property('mavenVersion')

// Task for Policy Stub Generation:
// 1. Run `gradlew genPolicyStubs` to generate policy stub files
// 2. Run `gradlew cleanGenPolicyStubs` to delete stub files
task genPolicyStubs(type: PolicyStubGenerationTask) {
    group 'Stub'
    description 'Generates sub classes for Sapphire policies'

    pkgName 'sapphire.policy'

    def src = new File("$buildDir/classes/java/main/sapphire/policy/")
    srcDir src
    inputs.dir src

    def dst = new File("src/main/java/sapphire/policy/stubs/")
    dstDir dst 
    outputs.dir dst
}

// Task for stub compilation
task compileStubs(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

genPolicyStubs.mustRunAfter compileJava
compileStubs.dependsOn genPolicyStubs
tasks.googleJavaFormat.mustRunAfter genPolicyStubs
check.dependsOn tasks.googleJavaFormat
jar.dependsOn compileStubs

task integTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}